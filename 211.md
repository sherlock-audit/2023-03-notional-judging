iglyx

medium

# BaseERC4626Proxy convert functions fail for empty Vault, not adhering to EIP-4626

## Summary

convertToShares() and convertToAssets() are unavailable when there is no Vault shares yet, while they should be per ERC4626 view functions specification.

## Vulnerability Detail

The case of empty Vault looks to be not covered in the implementation, while empty Vault revert isn't a part of EIP-4626 specification, so any downstream systems will not expect such behavior.

## Impact

This will affect any integrations, which can have their linked functionality fail with the corresponding unavailability impacts.

For example, a downstream system may call all the Vaults to gather TVL during the withdrawal life-cycle and will fail on BaseERC4626Proxy behavior, freezing the corresponding user funds for an arbitrary time.

## Code Snippet

_getTotalValueExternal() can be zero when there is no value/debt in the contract yet:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/nTokenERC20Proxy.sol#L57-L63

```solidity
    function _getTotalValueExternal() internal view override returns (uint256 totalValueExternal) {
        int256 underlyingInternal = NOTIONAL.nTokenPresentValueUnderlyingDenominated(currencyId);
@>      totalValueExternal = underlyingInternal
            // No overflow, native decimals is restricted to < 36 in initialize
            .mul(int256(10**nativeDecimals))
            .div(Constants.INTERNAL_TOKEN_PRECISION).toUint();
    }
```

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/PrimeCashProxy.sol#L60-L72

```solidity
    function _getTotalValueExternal() internal view override returns (uint256 totalValueExternal) {
        (
            PrimeRate memory pr,
            PrimeCashFactors memory factors,
            /* */,
            /* */
        ) = NOTIONAL.getPrimeFactors(currencyId, block.timestamp);

@>      totalValueExternal = pr.convertToUnderlying(factors.totalPrimeSupply.toInt())
            // No overflow, native decimals is restricted to < 36 in initialize
            .mul(int256(10**nativeDecimals))
            .div(Constants.INTERNAL_TOKEN_PRECISION).toUint();
    }
```

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/PrimeDebtProxy.sol#L61-L73

```solidity
    function _getTotalValueExternal() internal view override returns (uint256 totalValueExternal) {
        (
            PrimeRate memory pr,
            PrimeCashFactors memory factors,
            /* */,
            /* */
        ) = NOTIONAL.getPrimeFactors(currencyId, block.timestamp);

@>      totalValueExternal = pr.convertDebtStorageToUnderlying(factors.totalPrimeDebt.toInt().neg())
            // No overflow, native decimals is restricted to < 36 in initialize
            .mul(int256(10**nativeDecimals))
            .div(Constants.INTERNAL_TOKEN_PRECISION).neg().toUint();
    }
```

exchangeRate() will be zero in this case:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/BaseERC4626Proxy.sol#L303-L309

```solidity
    function exchangeRate() public view returns (uint256 rate) {
@>      uint256 totalValueExternal = _getTotalValueExternal();
        uint256 supply = _totalSupply();
        // Exchange Rate from token to Underlying in EXCHANGE_RATE_PRECISION is:
        // 1 token = totalValueExternal * EXCHANGE_RATE_PRECISION / totalSupply
@>      rate = totalValueExternal.mul(EXCHANGE_RATE_PRECISION).div(supply);
    }
```

If exchangeRate() is zero then convertToShares() will be reverted per division by zero:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/BaseERC4626Proxy.sol#L157-L160

```solidity
    /// @notice Converts an underlying token to an nToken denomination
    function convertToShares(uint256 assets) public override view returns (uint256 shares) {
        return assets.mul(EXCHANGE_RATE_PRECISION).div(exchangeRate());
    }
```

Also, exchangeRate() will revert if there is no shares yet, i.e. when _totalSupply() is zero:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/BaseERC4626Proxy.sol#L303-L309

```solidity
    function exchangeRate() public view returns (uint256 rate) {
        uint256 totalValueExternal = _getTotalValueExternal();
@>      uint256 supply = _totalSupply();
        // Exchange Rate from token to Underlying in EXCHANGE_RATE_PRECISION is:
        // 1 token = totalValueExternal * EXCHANGE_RATE_PRECISION / totalSupply
@>      rate = totalValueExternal.mul(EXCHANGE_RATE_PRECISION).div(supply);
    }
```

In this can both convertToShares() and convertToAssets() will be unavailable as both call exchangeRate():

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/BaseERC4626Proxy.sol#L162-L165

```solidity
    /// @notice Converts nToken denomination to underlying denomination
    function convertToAssets(uint256 shares) public override view returns (uint256 assets) {
        return exchangeRate().mul(shares).div(EXCHANGE_RATE_PRECISION);
    }
```

Notional V3 aims to provide view ERC4626 functions, including convertToShares() and convertToAssets(), in this release:

```md
The Notional V3 proxy will also deploy ERC20/ERC4626 compatible proxies for Prime Cash, nTokens and Prime Debt. All three should emit proper Transfer events for mints, burns and transfers. Existing Notional V2 nToken proxies do not emit proper Transfer events and cannot be upgraded. Full ERC4626 compatibility is not in this version (deposit, mint, withdraw, redeem are not fully functional) but view methods are implemented.
```

But per specification the reverts above should not happen:

https://eips.ethereum.org/EIPS/eip-4626

```md
*convertToShares*

The amount of shares that the Vault would exchange for the amount of assets provided, in an ideal scenario where all the conditions are met.

MUST NOT be inclusive of any fees that are charged against assets in the Vault.

MUST NOT show any variations depending on the caller.

MUST NOT reflect slippage or other on-chain conditions, when performing the actual exchange.

MUST NOT revert unless due to integer overflow caused by an unreasonably large input.

MUST round down towards 0.

This calculation MAY NOT reflect the “per-user” price-per-share, and instead should reflect the “average-user’s” price-per-share, meaning what the average user should expect to see when exchanging to and from.
```

## Tool used

Manual Review

## Recommendation

Consider treating `_totalSupply() == 0` and `_getTotalValueExternal() == 0` corner cases, for example:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/proxies/BaseERC4626Proxy.sol#L303-L309

```diff
    function exchangeRate() public view returns (uint256 rate) {
        uint256 totalValueExternal = _getTotalValueExternal();
        uint256 supply = _totalSupply();
        // Exchange Rate from token to Underlying in EXCHANGE_RATE_PRECISION is:
        // 1 token = totalValueExternal * EXCHANGE_RATE_PRECISION / totalSupply
-       rate = totalValueExternal.mul(EXCHANGE_RATE_PRECISION).div(supply);
+       rate = (supply == 0 || totalValueExternal == 0) ? EXCHANGE_RATE_PRECISION : totalValueExternal.mul(EXCHANGE_RATE_PRECISION).div(supply);
    }
```