iglyx

medium

# VaultAccountAction's exitVault updates the state after external call

## Summary

exitVault() first call depositUnderlyingExternal(), which returns extra funds with native token call, then vaultAccount state update happens. This setup can allow for reentrancy. 

## Vulnerability Detail

vaultAccount state is set after reentrancy point on the returning of the native funds, i.e. exitVault() -> redeemWithDebtRepayment() -> depositUnderlyingExternal() -> {some external actions} -> redeemWithDebtRepayment's `vaultAccount.setVaultAccount({vaultConfig: vaultConfig, checkMinBorrow: true})` execution chain is possible. This can be used to eliminate any state changes occurred during external calls, for example within VaultAccountAction and VaultLiquidationAction.

## Impact

At the moment there looks to be `nonReentrant` guards present on the all other state manipulation related functions, but on addition of any other VaultAccount state related functionality the straightforward exitVault() -> borrow on receiving ETH -> return to the flow and rewrite state with `setVaultAccount`, clearing the debt record, will be possible.

## Code Snippet

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/actions/VaultAccountAction.sol#L224-L283

```solidity
    function exitVault(
        address account,
        address vault,
        address receiver,
        uint256 vaultSharesToRedeem,
        uint256 lendAmount,
        uint32 minLendRate,
        bytes calldata exitVaultData
    ) external payable override nonReentrant returns (uint256 underlyingToReceiver) {
        ...

        // Puts a negative cash balance on the vault's temporary cash balance
        vaultAccount.lendToExitVault(
            vaultConfig, vaultState, lendAmount.toInt(), minLendRate, block.timestamp
        );
        vaultState.exitMaturity(vaultAccount, vaultConfig, vaultSharesToRedeem);

        // If insufficient strategy tokens are redeemed (or if it is set to zero), then
        // redeem with debt repayment will recover the repayment from the account's wallet
        // directly.
@>      underlyingToReceiver = underlyingToReceiver.add(vaultConfig.redeemWithDebtRepayment(
            vaultAccount, receiver, vaultSharesToRedeem, exitVaultData
        ));

        // Set the vault state after redemption completes
        vaultState.setVaultState(vaultConfig);

        if (vaultAccount.accountDebtUnderlying == 0 && vaultAccount.vaultShares == 0) {
            // If the account has no position in the vault at this point, set the maturity to zero as well
            vaultAccount.maturity = 0;
        }
@>      vaultAccount.setVaultAccount({vaultConfig: vaultConfig, checkMinBorrow: true});

        // It's possible that the user redeems more vault shares than they lend (it is not always the case
        // that they will be increasing their collateral ratio here, so we check that this is the case). No
        // need to check if the account has exited in full (maturity == 0).
        if (vaultAccount.maturity != 0) {
            IVaultAccountHealth(address(this)).checkVaultAccountCollateralRatio(vault, account);
        }
    }
```

redeemWithDebtRepayment() performs outbound transfer in-between state manipulation logic:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/vaults/VaultConfiguration.sol#L447-L466

```solidity
        if (amountTransferred < underlyingExternalToRepay) {
            // Recover any unpaid debt amount from the account directly
            uint256 residualRequired = underlyingExternalToRepay - amountTransferred;

            // actualTransferExternal is a positive number here to signify assets have entered
            // the protocol, excess ETH payments will be returned to the account
@>          (int256 actualTransferExternal, int256 primeCashDeposited) = TokenHandler.depositUnderlyingExternal(
                vaultAccount.account,
                vaultConfig.borrowCurrencyId,
                residualRequired.toInt(),
                vaultConfig.primeRate,
                false // excess ETH payments returned natively
            );
            amountTransferred = amountTransferred.add(actualTransferExternal.toUint());

            // Cash is held by the vault for debt repayment in this case.
            Emitter.emitTransferPrimeCash(
                vaultAccount.account, vaultConfig.vault, vaultConfig.borrowCurrencyId, primeCashDeposited
            );
        }
```

`returnNativeTokenWrapped` is false and residual funds are send out in the native form:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/balances/TokenHandler.sol#L176-L199

```solidity
    function depositUnderlyingExternal(
        address account,
        uint16 currencyId,
        int256 _underlyingExternalDeposit,
        PrimeRate memory primeRate,
        bool returnNativeTokenWrapped
    ) internal returns (int256 actualTransferExternal, int256 netPrimeSupplyChange) {
        uint256 underlyingExternalDeposit = _underlyingExternalDeposit.toUint();
        if (underlyingExternalDeposit == 0) return (0, 0);

        Token memory underlying = getUnderlyingToken(currencyId);
        if (underlying.tokenType == TokenType.Ether) {
            // Underflow checked above
            if (underlyingExternalDeposit < msg.value) {
                // Transfer any excess ETH back to the account
                GenericToken.transferNativeTokenOut(
@>                  account, msg.value - underlyingExternalDeposit, returnNativeTokenWrapped
                );
            } else {
                require(underlyingExternalDeposit == msg.value, "ETH Balance");
            }

            actualTransferExternal = _underlyingExternalDeposit;
        }
```

## Tool used

Manual Review

## Recommendation

Since returning the underlying to receiver isn't linked to the internal state, it can be done after all the updates. Now the fund transfer is inter-wired within a sequence of state updates, so the general recommendation here is to record what needs to be send out and do it as a last operation after all the updates.
