chaduke

medium

# getfCashGivenCashAmount() uses fCash_1 - fCash_0 == 0 as the convergence criteria, failing to consider rounding error.

## Summary
``getfCashGivenCashAmount()`` uses ``fCash_1 - fCash_0 == 0`` as the convergence criteria, failing to consider rounding error.  In practice, usually convergence only require a sufficiently small difference between ``fCash_1`` and ``fCash_0``.  


## Vulnerability Detail
``getfCashGivenCashAmount()``  allows a user to to use the secant method to converge on an ``fCash`` amount given the amount of cash.

[https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/markets/InterestRateCurve.sol#L536-L602](https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/markets/InterestRateCurve.sol#L536-L602)

 
The function  uses ``fCash_1 - fCash_0 == 0`` as the convergence criteria, however, due to rounding error, ``fCash_1`` and ``fCash_0`` might not be exactly equal to each other, and might off by 1 for example. In practice, usually convergence only require a sufficiently small difference between ``fCash_1`` and ``fCash_0``.  The following piece of code shows the possible rounding errors:

```javascript
            int256 fCash_n = fCash_1.sub(diff_1.mul(fCashDelta).div(diff_1.sub(diff_0)));
```

As a result, it is possible that fCash_1 - fCash_0 will be off by 1 but the function will revert instead of reporting a convergence. 


## Impact
``getfCashGivenCashAmount()`` uses ``fCash_1 - fCash_0 == 0`` as the convergence criteria, failing to consider rounding error.  As a result, 
 it is possible that when ``fCash_1`` is off by 1 from ``fCash_0`` but the function will revert instead of reporting a convergence.

## Code Snippet
See above

## Tool used
VSCode

Manual Review

## Recommendation
Introducing a ``delta`` for the difference of ``fCash_1`` and ``fCash_0``. As along as they are close to each other by ``delta``, we consider it is a convergence.

```diff
 function getfCashGivenCashAmount(
        InterestRateParameters memory irParams,
        int256 totalfCash,
        int256 netUnderlyingToAccount,
        int256 totalCashUnderlying,
        uint256 timeToMaturity
    ) internal pure returns (int256) {
        require(netUnderlyingToAccount != 0);
        // Cannot borrow more than total cash underlying
        require(netUnderlyingToAccount <= totalCashUnderlying, "Over Market Limit");

        int256 fCash_0;
        int256 fCash_1;
        {
            // Calculate fCash rate at the current mid point
            int256 currentfCashExchangeRate = _calculatePostFeeExchangeRate(
                irParams,
                totalfCash,
                totalCashUnderlying,
                timeToMaturity,
                netUnderlyingToAccount > 0 ? int256(-1) : int256(1) // set this such that we get the correct fee direction
            );

            if (netUnderlyingToAccount < 0) {
                // Lending
                // Minimum guess is lending at 0% interest, which means receiving fCash 1-1
                // with underlying cash amounts
                fCash_0 = netUnderlyingToAccount.neg();
                fCash_1 = netUnderlyingToAccount.mulInRatePrecision(currentfCashExchangeRate).neg();
            } else {
                // Borrowing
                fCash_0 = netUnderlyingToAccount.mulInRatePrecision(currentfCashExchangeRate).neg();
                fCash_1 = netUnderlyingToAccount.mulInRatePrecision(
                    getfCashExchangeRate(irParams.maxRate, timeToMaturity)
                ).neg();
            }
        }

        int256 diff_0 = _calculateDiff(
            irParams,
            totalfCash,
            totalCashUnderlying,
            fCash_0,
            timeToMaturity,
            netUnderlyingToAccount
        );

        for (uint8 i = 0; i < 250; i++) {
            int256 fCashDelta = (fCash_1 - fCash_0);
-            if (fCashDelta == 0) return fCash_1;
+           if(fCashDelta.abs() <= delta) return fCash_1;        // audit: if the difference is ignorable.
            int256 diff_1 = _calculateDiff(
                irParams,
                totalfCash,
                totalCashUnderlying,
                fCash_1,
                timeToMaturity,
                netUnderlyingToAccount
            );
            int256 fCash_n = fCash_1.sub(diff_1.mul(fCashDelta).div(diff_1.sub(diff_0)));

            // Assign new values for next comparison
            (fCash_1, fCash_0) = (fCash_n, fCash_1);
            diff_0 = diff_1;
        }

        revert("No convergence");
    }
```
