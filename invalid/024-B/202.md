xiaoming90

high

# Possible to create vault positions ineligible for liquidation

## Summary

Users can self-liquidate their secondary debt holdings in such a way that it is no longer possible to deleverage their vault account as `checkMinBorrow` will fail post-maturity.

## Vulnerability Detail

When deleveraging a vault account, the liquidator will pay down account debt directly and the account will not accrue any cash. Under most circumstances, it is not possible to put an account's debt below its minimum borrow size. 

However, there are _two_ exceptions to this:
 - Liquidators purchasing cash from a vault account. This only applies to non-prime vault accounts.
 - A vault account is being settled and `checkMinBorrow` is skipped to ensure an account can always be settled.

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultLiquidationAction.sol#L57-L119

```solidity
File: VaultLiquidationAction.sol
057:     function deleverageAccount(
058:         address account,
059:         address vault,
060:         address liquidator,
061:         uint16 currencyIndex,
062:         int256 depositUnderlyingInternal
063:     ) external payable nonReentrant override returns (
064:         uint256 vaultSharesToLiquidator,
065:         int256 depositAmountPrimeCash
066:     ) {
067:         require(currencyIndex < 3);
068:         (
069:             VaultConfig memory vaultConfig,
070:             VaultAccount memory vaultAccount,
071:             VaultState memory vaultState
072:         ) = _authenticateDeleverage(account, vault, liquidator);
073: 
074:         PrimeRate memory pr;
075:         // Currency Index is validated in this method
076:         (
077:             depositUnderlyingInternal,
078:             vaultSharesToLiquidator,
079:             pr
080:         ) = IVaultAccountHealth(address(this)).calculateDepositAmountInDeleverage(
081:             currencyIndex, vaultAccount, vaultConfig, vaultState, depositUnderlyingInternal
082:         );
083: 
084:         uint16 currencyId = vaultConfig.borrowCurrencyId;
085:         if (currencyIndex == 1) currencyId = vaultConfig.secondaryBorrowCurrencies[0];
086:         else if (currencyIndex == 2) currencyId = vaultConfig.secondaryBorrowCurrencies[1];
087: 
088:         Token memory token = TokenHandler.getUnderlyingToken(currencyId);
089:         // Excess ETH is returned to the liquidator natively
090:         (/* */, depositAmountPrimeCash) = TokenHandler.depositUnderlyingExternal(
091:             liquidator, currencyId, token.convertToExternal(depositUnderlyingInternal), pr, false 
092:         );
093: 
094:         // Do not skip the min borrow check here
095:         vaultAccount.vaultShares = vaultAccount.vaultShares.sub(vaultSharesToLiquidator);
096:         if (vaultAccount.maturity == Constants.PRIME_CASH_VAULT_MATURITY) {
097:             // Vault account will not incur a cash balance if they are in the prime cash maturity, their debts
098:             // will be paid down directly.
099:             _reduceAccountDebt(
100:                 vaultConfig, vaultState, vaultAccount, pr, currencyIndex, depositUnderlyingInternal, true
101:             );
102:             depositAmountPrimeCash = 0;
103:         }
104: 
105:         // Check min borrow in this liquidation method, the deleverage calculation should adhere to the min borrow
106:         vaultAccount.setVaultAccountForLiquidation(vaultConfig, currencyIndex, depositAmountPrimeCash, true);
107: 
108:         emit VaultDeleverageAccount(vault, account, currencyId, vaultSharesToLiquidator, depositAmountPrimeCash);
109:         emit VaultLiquidatorProfit(vault, account, liquidator, vaultSharesToLiquidator, true);
110: 
111:         _transferVaultSharesToLiquidator(
112:             liquidator, vaultConfig, vaultSharesToLiquidator, vaultAccount.maturity
113:         );
114: 
115:         Emitter.emitVaultDeleverage(
116:             liquidator, account, vault, currencyId, vaultState.maturity,
117:             depositAmountPrimeCash, vaultSharesToLiquidator
118:         );
119:     }
```

`currencyIndex` represents which currency is being liquidated and `depositUnderlyingInternal` the amount of debt being reduced. Only one currency's debt can be updated here.

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultLiquidationAction.sol#L239-L267

```solidity
File: VaultLiquidationAction.sol
239:     function _reduceAccountDebt(
240:         VaultConfig memory vaultConfig,
241:         VaultState memory vaultState,
242:         VaultAccount memory vaultAccount,
243:         PrimeRate memory primeRate,
244:         uint256 currencyIndex,
245:         int256 depositUnderlyingInternal,
246:         bool checkMinBorrow
247:     ) private {
248:         if (currencyIndex == 0) {
249:             vaultAccount.updateAccountDebt(vaultState, depositUnderlyingInternal, 0);
250:             vaultState.setVaultState(vaultConfig);
251:         } else {
252:             // Only set one of the prime rates, the other prime rate is not used since
253:             // the net debt amount is set to zero
254:             PrimeRate[2] memory pr;
255:             pr[currencyIndex - 1] = primeRate;
256: 
257:             VaultSecondaryBorrow.updateAccountSecondaryDebt(
258:                 vaultConfig,
259:                 vaultAccount.account,
260:                 vaultAccount.maturity,
261:                 currencyIndex == 1 ? depositUnderlyingInternal : 0,
262:                 currencyIndex == 2 ? depositUnderlyingInternal : 0,
263:                 pr,
264:                 checkMinBorrow
265:             );
266:         }
267:     }
```

In the case of vault settlement, through self-liquidation, users can setup their debt and cash holdings post-settlement, such that both `accountDebtOne` and `accountDebtTwo` are non-zero and less than `vaultConfig.minAccountSecondaryBorrow`. The objective would be to have zero primary debt and `Y` secondary debt and `X` secondary cash. Post-settlement, cash is used to offset debt (`Y - X < minAccountSecondaryBorrow`) and due to the lack of `checkMinBorrow` in `VaultAccountAction.settleVaultAccount()`, both secondary currencies can have debt holdings below the minimum amount.

Now when `deleverageAccount()` is called on a prime vault account, debts are paid down directly. However, if we are only able to pay down one secondary currency at a time, `checkMinBorrow` will fail in `VaultSecondaryBorrow.updateAccountSecondaryDebt()` because both debts are checked.

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/internal/vaults/VaultSecondaryBorrow.sol#L295-L299

```solidity
File: VaultSecondaryBorrow.sol
295:         if (checkMinBorrow) {
296:             // No overflow on negation due to overflow checks above
297:             require(accountDebtOne == 0 || vaultConfig.minAccountSecondaryBorrow[0] <= -accountDebtOne, "min borrow");
298:             require(accountDebtTwo == 0 || vaultConfig.minAccountSecondaryBorrow[1] <= -accountDebtTwo, "min borrow");
299:         }
```

No prime fees accrue on secondary debt, hence, this debt will never reach a point where it is above the minimum borrow amount.

## Impact

Malicious actors can generate vault accounts which cannot be liquidated. Through opening numerous vault positions, Notional can rack up significant exposure and accrue bad debt as a result.

## Code Snippet

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultLiquidationAction.sol#L57-L119

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultLiquidationAction.sol#L239-L267

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/internal/vaults/VaultSecondaryBorrow.sol#L295-L299

## Tool used

Manual Review

## Recommendation

Either allow for multiple currencies to be liquidated or ensure that `checkMinBorrow` is performed only on the currency which is being liquidated.