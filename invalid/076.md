chaduke

high

# storeAssets attempts to store portfolioState.newAssets.length instead of portfolioState.lastNewAssetIndex new assets into storage, leading to wrong assetStorageLength and possible REVERT.

## Summary
``storeAssets()`` attempts to store ``portfolioState.newAssets.length`` instead of ``portfolioState.lastNewAssetIndex`` new assets into storage, leading to possible wrong ``assetStorageLength`` and REVERT. 

## Vulnerability Detail

``storeAssets()`` takes a portfolio state and writes it to storage.
[https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/portfolio/PortfolioHandler.sol#L165-L261](https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/portfolio/PortfolioHandler.sol#L165-L261)

In addition to updating/deleting existing assets, it will use the array ``portfolioState.newAssets`` to add new assets to the storage. However, array ``portfolioState.newAssets`` is a dynamic array whose memory is allocated with double expansion each time the array is full. The actual number of elements in the array is tracked in ``portfolioState.lastNewAssetIndex``. Unfortunately, instead of using ``portfolioState.lastNewAssetIndex``, the function uses ``portfolioState.newAssets.length`` to iterate through each element in  ``portfolioState.newAssets``. This allows the function to read dirty memory - the portion of the array that is beyond ``portfolioState.lastNewAssetIndex`` but still within ``portfolioState.newAssets.length``.

```javascript
for (uint256 i = 0; i < portfolioState.newAssets.length; i++) {
            PortfolioAsset memory asset = portfolioState.newAssets[i];
            if (asset.notional == 0) continue;
            require(
                asset.storageState != AssetStorageState.Delete &&
                asset.storageState != AssetStorageState.RevertIfStored
            ); // dev: store assets deleted storage

            (hasDebt, portfolioActiveCurrencies, nextSettleTime) = _updatePortfolioContext(
                asset,
                hasDebt,
                portfolioActiveCurrencies,
                nextSettleTime
            );

            _storeAsset(asset, storageArray[assetStorageLength]);
            assetStorageLength += 1;
        }
```

When such dirty memory is read by the function, wrong and more assets will be written into the storage. In particular, ``assetStorageLength`` might become larger than it is supposed to be. In the case of ``assetStorageLength > 16``, the function will revert even though the total number of actual assets is no greater than 16 due to the following check:

```javascript
  require(assetStorageLength <= 16); // dev: active currencies bytes32 overflow
```



## Impact
``storeAssets()`` attempts to store ``portfolioState.newAssets.length`` instead of ``portfolioState.lastNewAssetIndex`` new assets into storage, leading to possible wrong ``assetStorageLength`` and REVERT.

## Code Snippet

## Tool used
VScode

Manual Review

## Recommendation
Change the for loop and use ``portfolioState.lastNewAssetIndex`` as the limit:

```diff
function storeAssets(PortfolioState memory portfolioState, address account)
        internal
        returns (
            bool,
            bytes32,
            uint8,
            uint40
        )
    {
        bool hasDebt;
        // NOTE: cannot have more than 16 assets or this byte object will overflow. Max assets is
        // set to 7 and the worst case during liquidation would be 7 liquidity tokens that generate
        // 7 additional fCash assets for a total of 14 assets. Although even in this case all assets
        // would be of the same currency so it would not change the end result of the active currency
        // calculation.
        bytes32 portfolioActiveCurrencies;
        uint256 nextSettleTime;

        for (uint256 i = 0; i < portfolioState.storedAssets.length; i++) {
            PortfolioAsset memory asset = portfolioState.storedAssets[i];
            // NOTE: this is to prevent the storage of assets that have been modified in the AssetHandler
            // during valuation.
            require(asset.storageState != AssetStorageState.RevertIfStored);

            // Mark any zero notional assets as deleted
            if (asset.storageState != AssetStorageState.Delete && asset.notional == 0) {
                deleteAsset(portfolioState, i);
            }
        }

        // First delete assets from asset storage to maintain asset storage indexes
        for (uint256 i = 0; i < portfolioState.storedAssets.length; i++) {
            PortfolioAsset memory asset = portfolioState.storedAssets[i];

            if (asset.storageState == AssetStorageState.Delete) {
                // Delete asset from storage
                uint256 currentSlot = asset.storageSlot;
                assembly {
                    sstore(currentSlot, 0x00)
                }
            } else {
                if (asset.storageState == AssetStorageState.Update) {
                    PortfolioAssetStorage storage assetStorage;
                    uint256 currentSlot = asset.storageSlot;
                    assembly {
                        assetStorage.slot := currentSlot
                    }

                    _storeAsset(asset, assetStorage);
                }

                // Update portfolio context for every asset that is in storage, whether it is
                // updated in storage or not.
                (hasDebt, portfolioActiveCurrencies, nextSettleTime) = _updatePortfolioContext(
                    asset,
                    hasDebt,
                    portfolioActiveCurrencies,
                    nextSettleTime
                );
            }
        }

        // Add new assets
        uint256 assetStorageLength = portfolioState.storedAssetLength;
        mapping(address => 
            PortfolioAssetStorage[MAX_PORTFOLIO_ASSETS]) storage store = LibStorage.getPortfolioArrayStorage();
        PortfolioAssetStorage[MAX_PORTFOLIO_ASSETS] storage storageArray = store[account];
-        for (uint256 i = 0; i < portfolioState.newAssets.length; i++) {
+       for (uint256 i = 0; i < portfolioState.lastNewAssetIndex; i++) {

            PortfolioAsset memory asset = portfolioState.newAssets[i];
            if (asset.notional == 0) continue;
            require(
                asset.storageState != AssetStorageState.Delete &&
                asset.storageState != AssetStorageState.RevertIfStored
            ); // dev: store assets deleted storage

            (hasDebt, portfolioActiveCurrencies, nextSettleTime) = _updatePortfolioContext(
                asset,
                hasDebt,
                portfolioActiveCurrencies,
                nextSettleTime
            );

            _storeAsset(asset, storageArray[assetStorageLength]);
            assetStorageLength += 1;
        }

        // 16 is the maximum number of assets or portfolio active currencies will overflow its
        // 32 bytes size given 2 bytes per currency
        require(assetStorageLength <= 16); // dev: active currencies bytes32 overflow
        require(nextSettleTime <= type(uint40).max); // dev: portfolio return value overflow
        return (
            hasDebt,
            portfolioActiveCurrencies,
            uint8(assetStorageLength),
            uint40(nextSettleTime)
        );
    }

```
