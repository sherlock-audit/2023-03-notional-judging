ShadowForce

high

# Trading action missing slippage control even allows trading being executed when netPrimeCash is 0

## Summary

Trading action missing slippage control even allows trading being executed when netPrimeCash is 0

## Vulnerability Detail

In TradingAction.sol, the function market.executeTrade is called in a few places!

for example, we are callling

```solidity
function executeVaultTrade(uint16 currencyId, address vault, bytes32 trade)
	external
	returns (int256 netPrimeCash) {
	CashGroupParameters memory cashGroup = CashGroup.buildCashGroupStateful(currencyId);
	MarketParameters memory market;
	TradeActionType tradeType = TradeActionType(uint256(uint8(bytes1(trade))));

	// During a vault trade, the vault executes the trade on behalf of the account
	(netPrimeCash, /* */) = _executeLendBorrowTrade(vault, cashGroup, market, tradeType, block.timestamp, trade);
}
```

calling  (netPrimeCash, /* */) = _executeLendBorrowTrade(vault, cashGroup, market, tradeType, block.timestamp, trade);

```solidity
   function _executeLendBorrowTrade(
        address account,
        CashGroupParameters memory cashGroup,
        MarketParameters memory market,
        TradeActionType tradeType,
        uint256 blockTime,
        bytes32 trade
    )
        private
        returns (
            int256 cashAmount,
            int256 fCashAmount
        )
    {
        uint256 marketIndex = uint256(uint8(bytes1(trade << 8)));
        // NOTE: this updates the market in memory
        cashGroup.loadMarket(market, marketIndex, false, blockTime);

        fCashAmount = int256(uint88(bytes11(trade << 16)));
        // fCash to account will be negative here
        if (tradeType == TradeActionType.Borrow) fCashAmount = fCashAmount.neg();

        cashAmount = market.executeTrade(
            account,
            cashGroup,
            fCashAmount,
            market.maturity.sub(blockTime),
            marketIndex
        );
```

note the function call market.executeTrade

```solidity
    function executeTrade(
        MarketParameters memory market,
        address account,
        CashGroupParameters memory cashGroup,
        int256 fCashToAccount,
        uint256 timeToMaturity,
        uint256 marketIndex
    ) internal returns (int256 netPrimeCash) {
        int256 netPrimeCashToReserve;
        (netPrimeCash, netPrimeCashToReserve) = InterestRateCurve.calculatefCashTrade(
            market,
            cashGroup,
            fCashToAccount,
            timeToMaturity,
            marketIndex
        );

        MarketStorage storage marketStorage = _getMarketStoragePointer(market);
        // @audit
        _setMarketStorage(
            marketStorage,
            market.totalfCash,
            market.totalPrimeCash,
            market.lastImpliedRate,
            market.oracleRate,
            market.previousTradeTime
        );
        BalanceHandler.incrementFeeToReserve(cashGroup.currencyId, netPrimeCashToReserve);

        Emitter.emitfCashMarketTrade(
            account, cashGroup.currencyId, market.maturity, fCashToAccount, netPrimeCash, netPrimeCashToReserve
        );
    }

```

the netPrimeCash is derived from calculatefCashTrade

note there is no slippage control at all

if we look at the function calculatefCashTrade that return the primeCash amount

```solidity

    /// @notice Calculates the asset cash amount the results from trading fCashToAccount with the market. A positive
    /// fCashToAccount is equivalent of lending, a negative is borrowing. Updates the market state in memory.
    /// @param market the current market state
    /// @param cashGroup cash group configuration parameters
    /// @param fCashToAccount the fCash amount that will be deposited into the user's portfolio. The net change
    /// to the market is in the opposite direction.
    /// @param timeToMaturity number of seconds until maturity
    /// @param marketIndex the relevant tenor of the market to trade on
    /// @return netAssetCash amount of asset cash to credit or debit to an account
    /// @return netAssetCashToReserve amount of cash to credit to the reserve (always positive)
    function calculatefCashTrade(
        MarketParameters memory market,
        CashGroupParameters memory cashGroup,
        int256 fCashToAccount,
        uint256 timeToMaturity,
        uint256 marketIndex
    ) internal view returns (int256, int256) {
        // Market index must be greater than zero
        require(marketIndex > 0);
        // We return false if there is not enough fCash to support this trade.
        // if fCashToAccount > 0 and totalfCash - fCashToAccount <= 0 then the trade will fail
        // if fCashToAccount < 0 and totalfCash > 0 then this will always pass
        if (market.totalfCash <= fCashToAccount) return (0, 0);

        InterestRateParameters memory irParams = getActiveInterestRateParameters(cashGroup.currencyId, marketIndex);
        int256 totalCashUnderlying = cashGroup.primeRate.convertToUnderlying(market.totalPrimeCash);

        // returns the net cash amounts to apply to each of the three relevant balances.
        (
            int256 netUnderlyingToAccount,
            int256 netUnderlyingToMarket,
            int256 netUnderlyingToReserve
        ) = _getNetCashAmountsUnderlying(
            irParams,
            market,
            cashGroup,
            totalCashUnderlying,
            fCashToAccount,
            timeToMaturity
        );

        // Signifies a failed net cash amount calculation
        if (netUnderlyingToAccount == 0) return (0, 0);

        {
            // Do not allow utilization to go above 100 on trading, calculate the utilization after
            // the trade has taken effect, meaning that fCash changes and cash changes are applied to
            // the market totals.
            market.totalfCash = market.totalfCash.subNoNeg(fCashToAccount);
            totalCashUnderlying = totalCashUnderlying.add(netUnderlyingToMarket);

            uint256 utilization = getfCashUtilization(0, market.totalfCash, totalCashUnderlying);
            if (utilization > uint256(Constants.RATE_PRECISION)) return (0, 0);

            uint256 newPreFeeImpliedRate = getInterestRate(irParams, utilization);

            // It's technically possible that the implied rate is actually exactly zero we will still
            // fail in this case. If this does happen we may assume that markets are not initialized.
            if (newPreFeeImpliedRate == 0) return (0, 0);

            // Saves the preFeeInterestRate and fCash
            market.lastImpliedRate = newPreFeeImpliedRate;
        }

        return _setNewMarketState(
            market,
            cashGroup.primeRate,
            netUnderlyingToAccount,
            netUnderlyingToMarket,
            netUnderlyingToReserve
        );
    }
```

we see it is possbile that this function can return 0!

```solidity
  if (market.totalfCash <= fCashToAccount) return (0, 0);
```

## Impact

Trading action missing slippage control even allows trading being executed when netPrimeCash is 0

the impact is severe, this not only impact the normal trade but impact the vaultAccount.sol such as borrowIntoVault.sol

```solidity
    function _borrowIntoVault(
        VaultAccount memory vaultAccount,
        VaultConfig memory vaultConfig,
        VaultState memory vaultState,
        uint256 maturity,
        int256 underlyingToBorrow,
        uint32 maxBorrowRate
    ) private {
        require(underlyingToBorrow < 0); // dev: fcash must be negative

        int256 primeCashBorrowed;
        if (maturity == Constants.PRIME_CASH_VAULT_MATURITY) {
            primeCashBorrowed = vaultConfig.primeRate.convertFromUnderlying(underlyingToBorrow).neg();
        } else {
            // fCash fees are assessed on the amount of cash borrowed after execution of the trade
            primeCashBorrowed = VaultConfiguration.executeTrade(
                vaultConfig.borrowCurrencyId,
                vaultConfig.vault,
                maturity,
                underlyingToBorrow,
                maxBorrowRate,
                vaultConfig.maxBorrowMarketIndex,
                block.timestamp
            );
            // Only assess fCash fees here, Prime Cash fees are assessed in a separate method
            vaultConfig.assessVaultFees(vaultAccount, primeCashBorrowed, maturity, block.timestamp);
        }
        require(primeCashBorrowed > 0, "Borrow failed");
```

calling

```solidity
    function executeTrade(
        uint16 currencyId,
        address vault,
        uint256 maturity,
        int256 netfCashToAccount,
        uint32 rateLimit,
        uint256 maxBorrowMarketIndex,
        uint256 blockTime
    ) internal returns (int256 netPrimeCash) {
        uint256 marketIndex = checkValidMaturity(currencyId, maturity, maxBorrowMarketIndex, blockTime);
        // fCash is restricted from being larger than uint88 inside the trade module
        uint256 fCashAmount = uint256(netfCashToAccount.abs());
        require(fCashAmount < type(uint88).max);

        // Encodes trade data for the TradingAction module
        bytes32 trade = bytes32(
            (uint256(uint8(netfCashToAccount > 0 ? TradeActionType.Lend : TradeActionType.Borrow)) << 248) |
            (uint256(marketIndex) << 240) |
            (uint256(fCashAmount) << 152) |
            (uint256(rateLimit) << 120)
        );

        // Use the library here to reduce the deployed bytecode size
        netPrimeCash = TradingAction.executeVaultTrade(currencyId, vault, trade);
    }
```

eventually we are still calling TradingAction.executeVaultTrade(currencyId, vault, trade), the code only let user specify maxBorrowRate or minLendRate but not primeCash output amount. in this case specifically the code check

```solidity
require(primeCashBorrowed > 0, "Borrow failed");
```

but not every function using market.executeTrade in TradingAction.sol

## Code Snippet

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/internal/markets/Market.sol#L75

## Tool used

Manual Review

## Recommendation

add slippage control and make sure that do not allow trade when netPrimeCash amoutn is 0

```solidity
   (netPrimeCash, netPrimeCashToReserve) = InterestRateCurve.calculatefCashTrade(
            market,
            cashGroup,
            fCashToAccount,
            timeToMaturity,
            marketIndex
        );
		
		// add code
		require(netPrimeCash > 0, "invalid net prime cash amount")

        MarketStorage storage marketStorage = _getMarketStoragePointer(market);
        // @audit
        _setMarketStorage(
            marketStorage,
            market.totalfCash,
            market.totalPrimeCash,
            market.lastImpliedRate,
            market.oracleRate,
            market.previousTradeTime
        );
```
